// This program is free software: you can redistribute it and/or modify
// it under the terms of the GNU Affero General Public License as
// published by the Free Software Foundation, either version 3 of the
// License, or (at your option) any later version.
//
// This program is distributed in the hope that it will be useful,
// but WITHOUT ANY WARRANTY; without even the implied warranty of
// MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
// GNU Affero General Public License for more details.
//
// You should have received a copy of the GNU Affero General Public License
// along with this program.  If not, see <https://www.gnu.org/licenses/>.

const std = @import("std");
const URL = @import("../core/browser/URL.zig");
const WaitGroup = @import("../support/wait_group.zig");
const runtime_io = @import("../support/io.zig");

const TestHTTPServer = @This();

shutdown: std.atomic.Value(bool),
listener: ?std.Io.net.Server,
handler: Handler,

const Handler = *const fn (req: *std.http.Server.Request) anyerror!void;

pub fn init(handler: Handler) TestHTTPServer {
    return .{
        .shutdown = .init(true),
        .listener = null,
        .handler = handler,
    };
}

pub fn deinit(self: *TestHTTPServer) void {
    self.listener = null;
}

pub fn stop(self: *TestHTTPServer) void {
    self.shutdown.store(true, .release);
    if (self.listener) |*listener| {
        listener.socket.close(runtime_io.get());
    }
}

pub fn run(self: *TestHTTPServer, wg: *WaitGroup) !void {
    var ready = false;
    defer if (!ready) wg.finish();
    const address = try std.Io.net.IpAddress.parse("127.0.0.1", 9582);

    self.listener = try address.listen(runtime_io.get(), .{ .reuse_address = true });
    var listener = &self.listener.?;
    self.shutdown.store(false, .release);

    wg.finish();
    ready = true;

    while (true) {
        const conn = listener.accept(runtime_io.get()) catch |err| {
            if (self.shutdown.load(.acquire) or err == error.SocketNotListening) {
                return;
            }
            return err;
        };
        const thrd = try std.Thread.spawn(.{}, handleConnection, .{ self, conn });
        thrd.detach();
    }
}

fn handleConnection(self: *TestHTTPServer, conn: std.Io.net.Stream) !void {
    defer conn.close(runtime_io.get());

    var req_buf: [2048]u8 = undefined;
    var write_buf: [2048]u8 = undefined;
    var conn_reader = conn.reader(runtime_io.get(), &req_buf);
    var conn_writer = conn.writer(runtime_io.get(), &write_buf);

    var http_server = std.http.Server.init(&conn_reader.interface, &conn_writer.interface);

    while (true) {
        var req = http_server.receiveHead() catch |err| switch (err) {
            error.ReadFailed => continue,
            error.HttpConnectionClosing => continue,
            else => {
                std.debug.print("Test HTTP Server error: {}\n", .{err});
                return err;
            },
        };

        self.handler(&req) catch |err| {
            std.debug.print("test http error '{s}': {}\n", .{ req.head.target, err });
            try req.respond("server error", .{ .status = .internal_server_error });
            return;
        };
    }
}

pub fn sendFile(req: *std.http.Server.Request, file_path: []const u8) !void {
    var url_buf: [1024]u8 = undefined;
    var fba = std.heap.FixedBufferAllocator.init(&url_buf);
    const unescaped_file_path = try URL.unescape(fba.allocator(), file_path);
    const io = runtime_io.get();
    var file = std.Io.Dir.cwd().openFile(io, unescaped_file_path, .{}) catch |err| switch (err) {
        error.FileNotFound => return req.respond("server error", .{ .status = .not_found }),
        else => return err,
    };
    defer file.close(io);

    const stat = try file.stat(io);
    var send_buffer: [4096]u8 = undefined;

    var res = try req.respondStreaming(&send_buffer, .{
        .content_length = stat.size,
        .respond_options = .{
            .extra_headers = &.{
                .{ .name = "content-type", .value = getContentType(file_path) },
            },
        },
    });

    var read_buffer: [4096]u8 = undefined;
    var reader = file.reader(io, &read_buffer);
    _ = try res.writer.sendFileAll(&reader, .unlimited);
    try res.writer.flush();
    try res.end();
}

fn getContentType(file_path: []const u8) []const u8 {
    if (std.mem.endsWith(u8, file_path, ".js")) {
        return "text/javascript";
    }

    if (std.mem.endsWith(u8, file_path, ".GB2312.html")) {
        return "text/html; charset=GB2312";
    }

    if (std.mem.endsWith(u8, file_path, ".html")) {
        return "text/html";
    }

    if (std.mem.endsWith(u8, file_path, ".htm")) {
        return "text/html";
    }

    if (std.mem.endsWith(u8, file_path, ".xml")) {
        // some wpt tests do this
        return "text/xml";
    }

    if (std.mem.endsWith(u8, file_path, ".mjs")) {
        // mjs are ECMAScript modules
        return "text/javascript";
    }

    std.debug.print("TestHTTPServer asked to serve an unknown file type: {s}\n", .{file_path});
    return "text/html";
}

test "TestHTTPServer: JavaScript resources use a script MIME type" {
    try std.testing.expectEqualStrings("text/javascript", getContentType("fixture.js"));
    try std.testing.expectEqualStrings("text/javascript", getContentType("fixture.mjs"));
}
